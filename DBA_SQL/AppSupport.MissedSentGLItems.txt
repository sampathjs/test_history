USE [DBA]
GO
/****** Object:  StoredProcedure [AppSupport].[MissedSentGLItems]    Script Date: 25/07/2018 10:25:57 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

ALTER PROC [AppSupport].[MissedSentGLItems] (@debug TINYINT = 0, @email_address VARCHAR(1000))

AS 
BEGIN

SET NOCOUNT ON

DECLARE @rowcount INT

IF OBJECT_ID('tempdb..##MissedSentGLItems') IS NOT NULL
DROP TABLE ##MissedSentGLItems;

-- Dynamic SQL to pick up the correct database name for cross-server deployment
-- Works only if there is a database beginning with "OLEM" on the DB Server
-- If no such database is found, the query runs for the prod database "OLEME00P"

DECLARE @db_name varchar(20)
declare @sql_stmt nvarchar(4000)

-- Top 1 as test/dev DB servers have multiple databases
-- If no such databases found, use the PROD DB name

SELECT TOP 1 @db_name = ISNULL(name,'OLEME00P')
FROM sys.databases
WHERE name like 'olem%'


SET @sql_stmt = 'SELECT ab.deal_tracking_num, ab.input_date, ati.value, t.name AS toolset, ccy.name AS currency, ts.name, ab.reference, ab.maturity_date, ab.last_update, p1.short_name as int_bu, p2.short_name as ext_bu
				INTO ##MissedSentGLItems
				FROM ' + @db_name + '.dbo.ab_tran ab
				INNER JOIN ' + @db_name + '.dbo.system_dates sd on 1=1
				INNER JOIN ' + @db_name + '.dbo.trans_status ts on ts.trans_status_id = ab.tran_status
				INNER JOIN ' + @db_name + '.dbo.toolsets t on t.id_number = ab.toolset
				INNER JOIN ' + @db_name + '.dbo.party p1 on p1.party_id = ab.internal_bunit
				INNER JOIN ' + @db_name + '.dbo.party p2 on p2.party_id = ab.external_bunit
				INNER JOIN ' + @db_name + '.dbo.currency ccy on ccy.id_number = ab.currency
				LEFT OUTER JOIN ' + @db_name + '.dbo.ab_tran_info ati on ati.tran_num = ab.tran_num and ati.[type_id] = 20024
				WHERE ab.tran_status IN (3,4,5) --Validated, Matured, Cancelled
				AND ab.toolset NOT IN (6,10, 35, 36) -- 10 Cash, 6 Loan Dep, 35 CallNot, 36 Commodity
				AND COALESCE(ati.value, '''') IN  (''Pending Sent'', ''Pending Cancelled'','''')
				AND ab.currency NOT IN (0) -- USD
				AND ab.tran_type IN (0) -- Trading
				AND ab.internal_bunit NOT IN (20007, 20001) -- JM PMM HK, JM PMM US
				and ab.external_bunit not in (20006, 20008, 20001)-- JM PMM UK, JM PM UK
				and ab.internal_bunit <> ab.external_bunit
				and ((ab.toolset != 15 and ab.input_date < sd.business_date) or (ab.toolset = 15 and ab.maturity_date  < sd.business_date))
				ORDER BY ab.input_date'

EXEC sp_executesql @sql_stmt

SELECT @rowcount = @@ROWCOUNT

IF @debug = 1 PRINT @rowcount

IF @rowcount > 0 
BEGIN

DECLARE @email_subject NVARCHAR(100)
DECLARE @email_query   NVARCHAR(2000)
DECLARE @profile_name SYSNAME

SET @email_subject = 'Warning - Missed Sent GL Items - FAILED'
  
SET @email_query = 'SELECT * from ##MissedSentGLItems'

IF @debug = 1 PRINT @email_query

SELECT  @profile_name =    name FROM msdb.dbo.sysmail_profile WHERE profile_id = 1

IF @debug = 0 
BEGIN
 EXEC msdb.dbo.sp_send_dbmail  @profile_name = @profile_name,@recipients = @email_address,@subject = @email_subject,@query = @email_query,@importance = 'HIGH',@attach_query_result_as_file = 1;
 IF OBJECT_ID('tempdb..##MissedSentGLItems') IS NOT NULL DROP TABLE ##MissedSentGLItems;
 RETURN(1)
END

END

DROP TABLE ##MissedSentGLItems

END